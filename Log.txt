func Fmt(data)
  var strParts = []
  for i = 0 .. data.Count()-1
    strParts.Add(
    ^  string.Format("{0}", data[i])
    ^)
  return string.Join("/", strParts)

func FmtFoes()
  ?foe.Count > 0
    return string.Format("n:{0}, {1}m - {2}/{3}hp", foe.Count, foe.distance, foe.id, foe.hp)

func LogStrat(y, l, colour)
  >`0,@y@,@colour@,strat: @l@

func LogBelowPlayer(l)
  for i = 0 .. l.Count()-1
    >o-20,@-7-i@,@l[i][0]@,@Fmt(l[i][1])@

func LogTopLeft(l)
  for i = 0 .. l.Count()-1
    >`0,@i+1@,@l[i][0]@: @Fmt(l[i][1])@

func LogLowerLeft(x, y, color, message)
  y = y + screen.h
  >`@x@,@y@,@color@,@message@

func LogLowerRight(x, y, color, message)
  x = x + screen.w
  y = y + screen.h
  >`@x@,@y@,@color@,@message@

func LogTopRight(x, messages)
  var longest = 0

  for i = 0 .. messages.Count()-1
    longest = math.Max(longest, string.Size(messages[i][1]))

  var msg
  var color
  for i = 0 .. messages.Count()-1
    color = messages[i][0]
    msg = messages[i][1]
    // x pos = 
    //   width (go to right of screen)
    // - longest string (make sure they all fit)
    // + offset (align to the right)
    var x = screen.w - longest + (longest - string.Size(msg))
    >`@x@,@i+1@,@color@,@msg@

// returns an array of [color, message]
func FormatCooldownLog(weapon)
  var canUse = item.GetCooldown(weapon) <= 0 & item.CanActivate()
  var msg = string.Format("{0}: {1}", weapon, item.GetCooldown(weapon))
  ?!item.CanActivate()
    return ["#red", msg]
  :?item.GetCooldown(weapon) > 200
    return ["#red", msg]
  :?item.GetCooldown(weapon) > 100 & item.GetCooldown(weapon) <= 200
    return [color.FromRGB(255,102,0), msg] // orange
  :?item.GetCooldown(weapon) > 0
    return ["#yellow", msg]
  :
    return ["#green", msg]

// --------------------------

var bossFinished
?time <= 10
  bossFinished = false
:?foe = boss & foe.hp <= 100
  bossFinished = true

LogTopLeft([
  ["level", [loc.id, loc.name, loc.stars+"â˜†"]],
  ["time ", [totaltime, time.FormatCasual(totaltime, true)]],
  ["foe  ", [FmtFoes()]],
])

LogBelowPlayer([
  ["#yellow",                ["l", item.left, item.left.gp]],
  [color.FromRGB(255,102,0), ["r", item.right, item.right.gp]],
])

?!bossFinished
  LogTopRight(
  ^  -15, [
  ^    FormatCooldownLog("blade"),
  ^    FormatCooldownLog("bardiche"),
  ^    FormatCooldownLog("quarterstaff"),
  ^    FormatCooldownLog("heavy_hammer"),
  ^  ]
  ^)
