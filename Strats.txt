// increase loot
func LootStar()
  ?pickup.distance < 10
    equipL star
    equipR tri
    >o-6,3,#yellow,Loot!!
    return true
  return false

// hoon around in-between the action
// returns true if applied, false otherwise
func Speedy(dist)
  ?foe.distance > dist
    equipL star
    equipR tri
    >o-6,3,#green,Lets go!
    return true
  return false

func BeFast(do_speedy, do_dash)
  ?do_speedy
    return Speedy(30)
  ?do_dash
    return Dash(11, 16)
  return false

func Dash(min_dist, max_dist)
  ?foe.distance >= min_dist & foe.distance <= max_dist
    equipR dash
    return true
  return false

// auto-use potion when hp < 30
func AutoPotion()
  ?hp < 30
    activate potion

var lastActivation = 0

// equip & activate a two-handed weapon, if the ability is ready
// and the foe is within the specified distance
func UseTwoHandedWeaponIfReady(weapon, dist)
  ?foe.distance <= dist & item.GetCooldown(weapon) <= 0 & item.CanActivate()
    equip @weapon@
    activate R
    lastActivation = time
    return true
  return false

// use blade of the fallen god if there are enough foes
func BigBlade(min_foes)
  ?foe.count >= min_foes
    return UseTwoHandedWeaponIfReady("blade", 30)
  return false


func UseAvailableTwoHandedWeapon()
  ?foe.distance <= 20 & time - lastActivation > 30
    ?UseTwoHandedWeaponIfReady("bardiche", 10)
      return true
    :?UseTwoHandedWeaponIfReady("quarterstaff", 10)
      return true
    :?UseTwoHandedWeaponIfReady("heavy_hammer", 10)
      return true
  :
    >o-6,5,#red,too far
