// global variables
var LAST_WEAPON_ACTIVATION = 0

// close distance to the foe
func Dash(min_dist, max_dist)
  ?foe.distance >= min_dist & foe.distance <= max_dist
    equipR dashing
    >o-6,5,#green,Dashing!
    return true
  :
    >o-6,5,#red,NOT Dashing!
    return false

// hoon around in-between the action
// returns true if applied, false otherwise
func Speedy(dist)
  ?foe.distance > dist
    equipL star
    equipR tri
    >o-6,3,#green,Lets go!
    return true
  return false

func BeFast(do_speedy, do_dash)
  >o-6,3,#yellow,Speedy: @foe.distance > 30 & do_speedy@
  >o-6,4,#yellow,Dash:   @foe.distance >= 11 & foe.distance <= 16 & do_dash@

  return (do_speedy & Speedy(30)) | (do_dash & Dash(11, 16))

func Pickpocket()
  // ^& buffs.string = "pick_pocket"
  ?item.CanActivate()
  ^& item.GetCooldown("skeleton_arm") = 0
  ^& foe.distance < 9 & foe.distance > 3
  ^& foe ! immune_to_physical
  ^& foe.hp > 0
    >o-6,5,#green,Lets get!!!!!
    equip skeleton_arm
    activate R
    return true
  >o-6,5,#red,Lets not get :(
  return false

// weapons ---------------------------------------------------

// equip & activate a two-handed weapon, if the ability is ready
// and the foe is within the specified distance
func UseTwoHandedWeaponIfReady(weapon, dist)
  ?foe.distance <= dist & item.GetCooldown(weapon) <= 0 & item.CanActivate()
    equip @weapon@
    activate R
    LAST_WEAPON_ACTIVATION = time
    return true
  return false

func UseAvailableTwoHandedWeapon()
  ?foe.distance <= 20 & time - LAST_WEAPON_ACTIVATION > 30
    ?UseTwoHandedWeaponIfReady("bardiche", 10)
      return true
    :?UseTwoHandedWeaponIfReady("quarterstaff", 10)
      return true
    :?UseTwoHandedWeaponIfReady("heavy_hammer", 10)
      return true
  return false

func UseAvailableLostWeapons()
  ?item.GetCooldown("mask") = 0
    activate R
    return true
  :?UseTwoHandedWeaponIfReady("blade", 10)
    return true
  return false

// weapons (lost) --------------------------------------------

// use blade of the fallen god if there are enough foes
func BigBlade(min_foes)
  ?foe.count >= min_foes
    return UseTwoHandedWeaponIfReady("blade", 20)
  return false

func MaskOfTheLost()
  equipR mask
  ?item.GetCooldown("mask") = 0 
    activate R
  return true

// Use the "right" key to trigger the blade of the fallen god
func ListenForBladeTrigger()
  ?key = rightBegin
    >0,10,Triggered Blade!
    equip blade
    activate R
    LAST_WEAPON_ACTIVATION = time
    return true
  return false

