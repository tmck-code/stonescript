// increase loot
func LootStar()
  ?pickup.distance < 10
    equipL star
    equipR tri
    >o-6,3,#yellow,Loot!!

// hoon around in-between the action
// returns true if applied, false otherwise
func Speedy(dist)
  ?foe.distance > dist
    equipL star
    equipR tri
    >o-6,3,#green,Lets go!
    return true
  return false

// auto-use potion when hp < 30
func AutoPotion()
  ?hp < 30
    activate potion

var lastActivation = 0

// equip & activate a two-handed weapon if the ability is ready
func UseTwoHandedWeaponIfReady(weapon)
  ?item.GetCooldown(weapon) <= 0 & item.CanActivate()
    equip @weapon@
    activate R
    lastActivation = time
    return true
  return false

// use blade of the fallen god if there are enough foes
func BigBlade(min_foes)
  ?foe.Count >= min_foes
    return UseTwoHandedWeaponIfReady("blade")
  return false


func UseAvailableTwoHandedWeapon()
  ?foe.distance <= 20 & time - lastActivation > 30
    ?UseTwoHandedWeaponIfReady("bardiche")
      return true
    :?UseTwoHandedWeaponIfReady("quarterstaff")
      return true
    :?UseTwoHandedWeaponIfReady("heavy_hammer")
      return true
  :
    >o-6,5,#red,too far