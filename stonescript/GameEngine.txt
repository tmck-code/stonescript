// each feature should provide at least 3 methods:
// - <FeatureName>Enabled() -> bool        > returns if the feature is enabled
// - _<FeatureName>Toggle() -> void       > toggles the feature
// - <FeatureName>Toggle(keybind) -> void  > toggles the feature when the keybind is pressed

// global variables -----------------------------

var AAC_ENABLED                = false
var AAC_FAST_ENABLED           = false
var CUSTOM_STONESCRIPT_ENABLED = true
var OVERRIDE_ENABLED           = false

// remote caching ------------------------------

func RemoteCacheToggle(keybind)
  ?key = keybind
    sys.cacheRemoteFiles = !sys.cacheRemoteFiles
    >0,10,Remote caching @sys.cacheRemoteFiles@

// Attack Animation Cancel ----------------------

func AAC()
  ?AAC_ENABLED = false
    return
  ?item.left.state = 3
    equipL *0 wand //trash
    equipL @item.left@
  ?item.right.state = 3
    equipR *0 shield //trash
    equip @item.right@

func AACEnabled()
  return AAC_ENABLED

func SetAACEnabled(enabled)
  AAC_ENABLED = enabled

func _AACToggle()
  AAC_ENABLED = !AAC_ENABLED
  >0,10,AAC_ENABLED: @AAC_ENABLED@

func AACToggle(keybind)
  ?key = keybind
    _AACToggle()

// Attack Animation Cancel (Fast) ---------------
// (a version that uses the triskellion)

func AACFast()
  ?AAC_FAST_ENABLED = false
    return
  ?item.right.state = 3
    ?totaltime % 2 = 0
      equipR trisk
    :
      equipR dash
    equip @item.right@
  ?item.left.state = 3
    equipL trisk
    equipL @item.left@

func AACFastEnabled()
  return AAC_FAST_ENABLED

func SetAACFastEnabled(enabled)
  AAC_FAST_ENABLED = enabled

func _AACFastToggle()
  AAC_FAST_ENABLED = !AAC_FAST_ENABLED
  >0,10,AAC_FAST_ENABLED: @AAC_FAST_ENABLED@

func AACFastToggle(keybind)
  ?key = keybind
    _AACFastToggle()

// Loading Custom Stonescript -------------------

func CustomStonescriptEnabled()
  return CUSTOM_STONESCRIPT_ENABLED

func SetCustomStonescriptEnabled(enabled)
  CUSTOM_STONESCRIPT_ENABLED = enabled

func _CustomStonescriptToggle()
  CUSTOM_STONESCRIPT_ENABLED = !CUSTOM_STONESCRIPT_ENABLED
  >0,10,CUSTOM_STONESCRIPT_ENABLED: @CUSTOM_STONESCRIPT_ENABLED@

func CustomStonescriptToggle(keybind)
  ?key = keybind
    _CustomStonescriptToggle()

// Run Override ---------------------------------
// (custom hehaviour, usually for special event challenges)

func OverrideEnabled()
  return OVERRIDE_ENABLED

func SetOverrideEnabled(enabled)
  OVERRIDE_ENABLED = enabled

func _OverrideToggle()
  OVERRIDE_ENABLED = !OVERRIDE_ENABLED
  >0,10,OVERRIDE_ENABLED: @OVERRIDE_ENABLED@

func OverrideToggle(keybind)
  ?key = keybind
    _OverrideToggle()