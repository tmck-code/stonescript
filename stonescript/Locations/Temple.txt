var log = import SS/Log
var strats = import SS/Strats


func Temple()
  log.LogStrat(1, "Temple", "#green")

  ?strats.canUseBladeDS(13)
    strats.useBladeDS()

  ?foe = boss & foe = acronian_cultist
    ?strats._Speedy(16, false)
      return
    ?strats.Dash()
      return
    activate potion
    equipL ice hammer
    equipR ice sword
    return

  ?foe = boss & foe = nagaraja
    ?foe.state = 112 & foe.time = 59
      equipR mind
    equipR ice sword
    ?hp < 10
      equip vigor crossbow
      return
    ?strats._Speedy(16, false)
      return
    activate potion
    equipL ice hammer
    return

  ?strats._Speedy(22, false)
    return
  
  ?foe.count > 8
    equip ice crossbow
    return

  ?(foe.distance < 18 & foe.count <= 8) | foe = cult_marksman | foe.count <= 3
    ?strats.Dash()
      return

  ?(foe = heavy_hitter | foe = worm_rider) & foe.distance > 16
    equip ice crossbow
    return

  ?(foe.distance < 18 & foe.count <= 8) | foe = cult_marksman | foe = cult_sorcerer | foe.count <= 3
    ?strats._Dash(12, 18)
      return

  ?foe.distance > 8 & foe ! cult_marksman
    equip ice crossbow
    return

  ?strats.Dash()
    return

  equipL ice hammer
  equipR big ice sword

?loc = temple
  Temple()
