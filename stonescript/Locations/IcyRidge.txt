var log    = import SS/Log
var strats = import SS/Strats

var needShield = false
var IS_FINAL_BOSS_STAGE = false
?loc.loop = true
  IS_FINAL_BOSS_STAGE = false

func QuickShield()
  // about to be hit! equip the shield
  ?foe.state = 33 & foe.time > 10
    equip compound shield
    needShield = true
    return true
  // was just hit a few frames ago, equip the shield
  // for 2 frames during the next state, just in case (?)
  ?needShield = true & foe.time < 2
    equip compound shield
    return true
  // otherwise, disable the shield flag
  :
    needShield = false
    return false

func IcyRidge()
  log.LogStrat(4, "IcyRidge", "#green")

  ?strats.canUseBladeDS(2)
    strats.useBladeDS()

  ?foe = yeti & !IS_FINAL_BOSS_STAGE
    IS_FINAL_BOSS_STAGE = true

  ?IS_FINAL_BOSS_STAGE & foe.time > 400
    ?strats.Speedy() = true
      return

    ?foe.armor <= 0
      ?strats.MaskOfTheLost() = true
        return
    ?hp < 50
      ?strats.UseWandAbility("explosive", "L")
        return
      ?strats.UseWandAbility("calamity", "L")
        return
      ?strats.UseWandAbility("reset", "L")
        return

    equip fire wand +12
    equipR fire wand +11
    return

  ?strats._Speedy(19) = true
    return

  ?foe = ice_elemental | foe.distance > 18
    equip fire crossbow
    return
  
  ?foe.distance > 12
    equipL tri
    equipR fire wand +12
    return


  ?foe.distance > 5
    equipR fire wand +11
    equipL fire wand +12
    return

  ?strats.Dash() = true
    return

  equipL fire sword
  equipR compound shield
  return

?loc = icy_ridge
  IcyRidge()
