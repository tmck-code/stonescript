var log    = import SS/Log
var strats = import SS/Strats

var needShield = false

func IcyRidge()
  log.LogStrat(4, "IcyRidge", "#green")

  ?strats.canUseBladeDS(2)
    strats.useBladeDS()

  ?needShield = true & foe.time < 2
    equip compound shield
    return
  :
    needShield = false

  ?foe.state = 33 & foe.time > 10
    equip compound shield
    needShield = true
    return

  ?foe = boss
    ?strats.Speedy()
      return

    ?foe.state = 33 & foe.time > 10
      equip compound shield
      return

    ?foe.distance > 12
      strats.UseWeaponIfDist("fissure", 18)
      equip fire crossbow
      return

    ?foe.distance <= 5
      strats.UseAvailableTwoHandedWeapon()
      equipL fire hammer
      equipR compound shield
      return

    equipL fire wand *9
    equipR fire wand *10
    return

  ?strats.Speedy()
    return

  ?foe = ice_elemental | foe.distance > 10
    equip fire crossbow
    return

  ?strats.Dash()
    return

  :?foe.distance <= 5
    strats.UseAvailableTwoHandedWeapon()
    equipL fire hammer
    equipR compound shield
    return

  equipL fire wand *9
  equipR fire wand *10

?loc = icy_ridge
  IcyRidge()
