var log    = import SS/Log
var strats = import SS/Strats
var buffs = import SS/Checks/Buffs

buffs._UpdateBuffs()

var needShield = false
var IS_FINAL_BOSS_STAGE = false
var BOSS_TIME = 0
?loc.loop = true
  IS_FINAL_BOSS_STAGE = false
  BOSS_TIME = 0

// >`0,10,#white,BOSS_TIME @IS_FINAL_BOSS_STAGE@ @BOSS_TIME@


func QuickShield()
  // about to be hit! equip the shield
  ?foe.state = 33 & foe.time > 10
    equip compound shield
    needShield = true
    return true
  // was just hit a few frames ago, equip the shield
  // for 2 frames during the next state, just in case (?)
  ?needShield = true & foe.time < 2
    equip compound shield
    return true
  // otherwise, disable the shield flag
  :
    needShield = false
    return false

func IcyRidge()
  log.LogStrat(4, "IcyRidge", "#green")

  ?strats.canUseBladeDS(2)
    strats.useBladeDS()

  ?foe = yeti & !IS_FINAL_BOSS_STAGE
    IS_FINAL_BOSS_STAGE = true

  ?foe = giant_ice_elemental
    ?foe.debuffs.string ! damage
      equipR poison wand
      equipL fire wand +13
      return
    equipR fire wand +13
    return

  ?IS_FINAL_BOSS_STAGE
    BOSS_TIME = BOSS_TIME + 1

    activate potion

    ?foe = ice_wall
      equip fire crossbow
      return

    ?strats.Speedy()
      return

    ?BOSS_TIME > 400
      ?foe = yeti
        ?strats.MaskOfTheLost()
          return
      
      ?foe.hp > 3000 & foe.hp < 3200
        ?strats.UseWandAbility("frost", "L")
          return

      ?foe.hp < 3000
        ?strats.UseWandAbility("explosive", "L")
          return
        ?strats.UseWandAbility("plague", "R")
          return

    ?strats.Dash()
      return

    ?buffs.GetFoeDebuffLevel("dot") < 1
      equipR poison wand
      equipL fire wand +13
      return
    :
      equipR fire sword
      equipL fire hammer
      return

  ?strats.Speedy()
    return

  // ?foe.count > 8
  //   equipR fire talisman
  //   equipL fire wand +13
  ?foe = ice_elemental
    equipL fire talisman
    activate L
    equip fire crossbow
    return
  
  ?foe.distance > 15
    equip fire crossbow
    return

  ?strats.Dash()
    return

  ?foe = ice_pillar & foe.count < 4
    equipR poison wand
    equipL fire wand +13
    return


  ?foe.distance > 12
    equipR poison wand
    equipL fire wand +13
    return

  equipL fire sword
  equipR compound shield
  return

?loc = icy_ridge
  IcyRidge()
