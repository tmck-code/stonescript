var log    = import SS/Log
var strats = import SS/Strats

var needShield = false

func QuickShield()
  // about to be hit! equip the shield
  ?foe.state = 33 & foe.time > 10
    equip compound shield
    needShield = true
    return true
  // was just hit a few frames ago, equip the shield
  // for 2 frames during the next state, just in case (?)
  ?needShield = true & foe.time < 2
    equip compound shield
    return true
  // otherwise, disable the shield flag
  :
    needShield = false
    return false

func Override()
  equipL explosive wand
  equipR vigor wand
  // ?foe.distance > 20 & (foe ! boss & foe ! ice wall)
  //   equipR tri
  ?foe = boss & foe.time > 400
    ?item.GetCooldown("wand_fire") = 0
    ^ & item.CanActivate()
    ^ & armor > 14
      activate L
    ?item.GetCooldown("wand_vigor") = 0
    ^ & item.CanActivate()
    ^ & armor > 14
      activate R
  return false

func IcyRidge()
  log.LogStrat(4, "IcyRidge", "#green")

  ?Override()
    return

  ?strats.canUseBladeDS(2)
    strats.useBladeDS()

  ?foe.count > 5
    ?strats.MaskOfTheLost() = true
      return

  ?foe = boss
    ?strats.Speedy() = true
      return

    ?strats.MaskOfTheLost() = true
      return

    ?QuickShield() = true
      return

    activate potion

    ?item.GetCooldown("wand_fire") = 0 & item.CanActivate()
      equipL fire wand *9
      equipR fire wand *10
      activate R
      return

    ?foe.distance > 12
      strats.UseWeaponIfDist("fissure", 18)
      equip fire crossbow
      return

    equipL fire sword +8
    equipR compound shield
    return

  ?strats.Speedy() = true
    return

  ?foe = ice_elemental | foe.distance > 15 | foe = ice wall
    equip fire crossbow
    return

  ?strats.Dash() = true
    return

  equipL fire sword +8
  equipR compound shield
  return

?loc = icy_ridge
  IcyRidge()
