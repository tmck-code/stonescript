var log = import SS/Log
var strats = import SS/Strats
var canUse = import SS/Checks/CanUse


var BUTTON_MORE_MAX_DISTANCE = 0
var BUTTON_LESS_MAX_DISTANCE = 0
var BUTTON_MORE_MIN_DISTANCE = 0
var BUTTON_LESS_MIN_DISTANCE = 0
var MAX_DISTANCE = 20
var MIN_DISTANCE = 2

func DecreaseMaxDistance()
  MAX_DISTANCE = MAX_DISTANCE - 1
  BUTTON_LESS_MAX_DISTANCE.text = string.Format("<{0}", MAX_DISTANCE)
  BUTTON_MORE_MAX_DISTANCE.text = string.Format("{0}>", MAX_DISTANCE)

func IncreaseMaxDistance()
  MAX_DISTANCE = MAX_DISTANCE + 1
  BUTTON_MORE_MAX_DISTANCE.text = string.Format("{0}>", MAX_DISTANCE)
  BUTTON_LESS_MAX_DISTANCE.text = string.Format("<{0}", MAX_DISTANCE)

func DecreaseMinDistance()
  MIN_DISTANCE = MIN_DISTANCE - 1
  BUTTON_LESS_MIN_DISTANCE.text = string.Format("<{0}", MIN_DISTANCE)
  BUTTON_MORE_MIN_DISTANCE.text = string.Format("{0}>", MIN_DISTANCE)

func IncreaseMinDistance()
  MIN_DISTANCE = MIN_DISTANCE + 1
  BUTTON_MORE_MIN_DISTANCE.text = string.Format("{0}>", MIN_DISTANCE)
  BUTTON_LESS_MIN_DISTANCE.text = string.Format("<{0}", MIN_DISTANCE)

?loc.begin | loc.loop
  BUTTON_LESS_MAX_DISTANCE = ui.AddButton()
  BUTTON_MORE_MAX_DISTANCE = ui.AddButton()
  BUTTON_LESS_MIN_DISTANCE = ui.AddButton()
  BUTTON_MORE_MIN_DISTANCE = ui.AddButton()
  BUTTON_LESS_MIN_DISTANCE.anchor = bottom_left
  BUTTON_LESS_MIN_DISTANCE.dock = bottom_left
  BUTTON_LESS_MIN_DISTANCE.y = BUTTON_LESS_MIN_DISTANCE.y - 2
  BUTTON_LESS_MIN_DISTANCE.x = BUTTON_LESS_MIN_DISTANCE.x + 6
  BUTTON_LESS_MIN_DISTANCE.text = string.Format("<{0}", MIN_DISTANCE)
  BUTTON_LESS_MIN_DISTANCE.h = 3
  BUTTON_LESS_MIN_DISTANCE.w = 6
  BUTTON_LESS_MIN_DISTANCE.SetPressed(DecreaseMinDistance)
  
  BUTTON_MORE_MIN_DISTANCE.anchor = bottom_left
  BUTTON_MORE_MIN_DISTANCE.dock = bottom_left
  BUTTON_MORE_MIN_DISTANCE.y = BUTTON_MORE_MIN_DISTANCE.y - 2
  BUTTON_MORE_MIN_DISTANCE.x = BUTTON_LESS_MIN_DISTANCE.x + 6
  BUTTON_MORE_MIN_DISTANCE.text = string.Format("{0}>", MIN_DISTANCE)
  BUTTON_MORE_MIN_DISTANCE.h = 3
  BUTTON_MORE_MIN_DISTANCE.w = 6
  BUTTON_MORE_MIN_DISTANCE.SetPressed(IncreaseMinDistance)


  BUTTON_LESS_MAX_DISTANCE.anchor = bottom_left
  BUTTON_LESS_MAX_DISTANCE.dock = bottom_left
  BUTTON_LESS_MAX_DISTANCE.y = BUTTON_LESS_MAX_DISTANCE.y - 2
  BUTTON_LESS_MAX_DISTANCE.x = BUTTON_MORE_MIN_DISTANCE.x + 6
  BUTTON_LESS_MAX_DISTANCE.text = string.Format("<{0}", MAX_DISTANCE)
  BUTTON_LESS_MAX_DISTANCE.h = 3
  BUTTON_LESS_MAX_DISTANCE.w = 6
  BUTTON_LESS_MAX_DISTANCE.SetPressed(DecreaseMaxDistance)

  BUTTON_MORE_MAX_DISTANCE.anchor = bottom_left
  BUTTON_MORE_MAX_DISTANCE.dock = bottom_left
  BUTTON_MORE_MAX_DISTANCE.y = BUTTON_MORE_MAX_DISTANCE.y - 2
  BUTTON_MORE_MAX_DISTANCE.x = BUTTON_LESS_MAX_DISTANCE.x + 6
  BUTTON_MORE_MAX_DISTANCE.text = string.Format("{0}>", MAX_DISTANCE)
  BUTTON_MORE_MAX_DISTANCE.h = 3
  BUTTON_MORE_MAX_DISTANCE.w = 6
  BUTTON_MORE_MAX_DISTANCE.SetPressed(IncreaseMaxDistance)

var IS_FINAL_BOSS_STAGE = false
var BOSS_TIME = 0
?loc.loop = true
  IS_FINAL_BOSS_STAGE = false
  BOSS_TIME = 0

>`0,10,#white,BOSS_TIME @IS_FINAL_BOSS_STAGE@ @BOSS_TIME@

var foeState = 0

func Caves()
  log.LogStrat(4, "Caves "+loc.stars+"â˜†", "#green")

  ?strats.canUseBladeDS(2)
    strats.useBladeDS()

  ?foe = spider_boss
    IS_FINAL_BOSS_STAGE = true

  ?IS_FINAL_BOSS_STAGE
    BOSS_TIME = BOSS_TIME + 1
    ?BOSS_TIME > 800
      ?armor < 14
        activate potion
      ?strats.UseWandAbility("explosive", "L")
        return
      ?strats.UseWandAbility("frost", "R")
        return
    equip ice crossbow
    return

  ?strats.Speedy()
    return

  ?foe.count > 8
    ?armor > 14
      ?strats.UseWandAbility("calamity", "L")
        return
    :
      equip ice wand
      equip frost wand
      return

  ?foe.distance >= MAX_DISTANCE & foe.count < 8
    equip ice crossbow
    return

  ?foe.distance >= MIN_DISTANCE
    equipL ice wand
    equipR vigor wand
    return

  equipL ice hammer
  equipR vigor sword

?loc = caves
  Caves()
