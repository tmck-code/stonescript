var log = import SS/Log
// var info = import Data/Info
var strats = import SS/Strats
var CanUse = import SS/Checks/CanUse
var gameEngine = import SS/GameEngine

var BOSS = false
var MINIBOSS = false
var equippedWeapon = "crossbow"
var weaponCycles = 0

?loc.begin | loc.loop
  BOSS = false
  MINIBOSS = false
  weaponCycles = 0

func FoeImmuneTo(element)
  ?string.IndexOf(foe.buffs.string, string.Format("adaptive_defense_{0}", element)) > 0
    return true
  ?string.IndexOf(foe.buffs.string, element) > 0
    return true
  return false

func Override()
  equip vigor staff
  ?CanUse.AbilityOfStaff("prevention")
    activate R
    return true
  return true

func HauntedHalls()
  log.LogStrat(1, "HauntedHalls", "#green")

  ?loc.loop
    BOSS = false
  >`0,13,#green,BOSS:@BOSS@
  // >`0,14,#green,MINIBOSS:@MINIBOSS@
  // >`0,15,#green,weaponCycles:@weaponCycles@ (@equippedWeapon@)

  ?strats.canUseBladeDS(1)
    strats.useBladeDS()
    return
  
  ?strats.MaskOfTheLost()
    return

  ?foe = boss & foe ! skeleton_boss & !MINIBOSS
    MINIBOSS = true
    ?armor > 14
      strats.UseWandAbility("plague", "L")
    equip vigor crossbow
    equippedWeapon = "crossbow"
    weaponCycles = 0
    return

  ?MINIBOSS & pos.x > 480
    MINIBOSS = false
    return

  ?MINIBOSS
    ?strats.Speedy()
      return
    ?item.right.state = 2 & (item.right = crossbow | item.right = poison wand)
      weaponCycles = weaponCycles + 1
      return
    ?weaponCycles > 2
      weaponCycles = 0
      ?equippedWeapon = "crossbow"
        equipL vigor wand
        equipR poison wand
        equippedWeapon = "wand"
        return
      :
        equip vigor crossbow
        equippedWeapon = "crossbow"
        return
    equipL vigor wand
    equipR poison wand
    return

  ?foe = skeleton_boss & BOSS = true & foe.hp = 0
    BOSS = false
    return

  ?foe = skeleton_boss & !BOSS
    BOSS = true
    return

  ?BOSS
    equipL big vigor sword
    equipR vigor sword
    ?foe.armor > 0
      equip heavy crossbow
      return

    ?(foe.state = 32 | foe.state = 33) & boss = phase2
      equipL vigor wand
      equipR compound shield
      return
    ?foe = ghost | foe = "immune_to_physical"
      equip vigor wand
      equipR poison wand
      return

    ?!foe.debuffs.string = chill
      equipR ice wand dI
      equipL fire wand df
      return

    ?boss = phase2
      equipL big vigor sword
      equipR vigor sword
      activate potion
      ?strats.UseWandAbility("plague", "R")
        return
      ?strats.UseWandAbility("explosive", "L")
        return

    // equip vigor crossbow
    // equip repeating crossbow
    equipL big vigor sword
    equipR vigor sword
    return

  ?strats._Speedy(22, false)
    return

  ?foe.distance > 15
    ?foe = ghost
      equipL vigor wand
      equipR reset wand +10
    :
      equip vigor crossbow
      return

  ?foe.count > 10
    ?strats.UseWandAbility("calamity", "L")
      return

  ?foe.count < 8 & foe ! ghost
    ?strats.Dash()
      return

  // ?foe.distance > 20
  //   equipL tri
  //   equipR vigor wand +12
  //   return

  equipL vigor wand +12
  equipR reset wand +10

?loc = halls
  ?gameEngine.OverrideEnabled() & Override()
    return
  HauntedHalls()
