var structs = import SS/Data/Structs

func longestLength(l)
  var longest = 0
  for i = 0 .. l.Count()-1
    longest = math.Max(longest, string.Size(l[i]))
  return longest


var ELEMENT_NAMES =   ["vigor", "ice", "fire", "poison", "aether", "stone"]
var ELEMENT_SYMBOLS = ["❤",     "❄",   "φ",    "∞",       "*",     "ʘ"]

func ElementSymbol(elementName)
  var i = ELEMENT_NAMES.IndexOf(string.ToLower(elementName))
  ?i ! -1
    return ELEMENT_SYMBOLS[i]
  return "?"

func LogStrat(y, l, colour)
  >`0,@y@,@colour@,strat: @l@

// e.g. LogAbovePlayer(["#red", "attack"], 0, 0, "right")
func LogAbovePlayer(l, x_offset, y_offset, increment, alignment)
  ?l.Count() = 0
    return
  
  for i = 0 .. l.Count()-1
    var colour = l[i][0]
    var msg = l[i][1]
    var y = (i*increment)+y_offset
    ?alignment = "right"
      >h@x_offset-string.Size(msg)@,@y@,@colour@,@msg@
    :?alignment = "left"
      >h@x_offset@,@y@,@colour@,@msg@

func LogTopLeft(y_offset, l)
  ?l.Count() = 0
    return
  for i = 0 .. l.Count()-1
    var colour = l[i][0]
    var msg = l[i][1]
    >`0,@y_offset+i+1@,@colour@,@msg@

func LogLowerLeft(x, y, color, message)
  y = y + screen.h
  >`@x@,@y@,@color@,@message@

func LogLowerRight(y_offset, messages)
  ?messages.Count() = 0
    return
  var longest = 0
  for i = 0 .. messages.Count()-1
    longest = math.Max(longest, string.Size(messages[i][1]))
  var msg
  var color
  for i = 0 .. messages.Count()-1
    color = messages[i][0]
    msg = messages[i][1]
    // x pos = 
    //   width (go to right of screen)
    // - longest string (make sure they all fit)
    // + offset (align to the right)
    var x = screen.w - longest + (longest - string.Size(msg))
    var y = screen.h - messages.Count() + i - y_offset
    >`@x@,@y@,@color@,@msg@

func LogTopRight(y_offset, messages)
  ?messages.Count() = 0
    return
  var longest = 0
  for i = 0 .. messages.Count()-1
    longest = math.Max(longest, string.Size(messages[i][1]))
  var msg
  var color
  for i = 0 .. messages.Count()-1
    color = messages[i][0]
    msg = messages[i][1]
    // x pos = 
    //   width (go to right of screen)
    // - longest string (make sure they all fit)
    // + offset (align to the right)
    var x = screen.w - longest + (longest - string.Size(msg))
    >`@x@,@y_offset+i+1@,@color@,@msg@

// ---------------------------------------------------
// Format (Fmt) functions:
// Format messages/stats/any info for logging

// FmtValue formats a value safely for logging. Recursively traverses
// arrays to format all values within them.
// Avoids "string.Join() can only join an array of strings"
// e.g.
// - individual values: FmtValue(1)                -> "1"
// - mixed arrays:      FmtValue([1,2,3,"abc"])    -> "[1,2,3,abc]"
// - nested arrays:     FmtValue([1,2,[3,4,"aa"]]) -> "[1,2,[3,4,aa]]"
func _Fmt(value, arr_sep)
  var msg = []
  ?Type(value) ! array
    return string.Format("{0}", value)
  ?value.Count() = 0
    return "[]"

  for i = 0 .. value.Count()-1
    ?Type(value) = array
      msg.Add(FmtValue(value[i]))
    :
      msg.Add(string.Format("{0}", value[i]))
  return "["+string.Join(arr_sep, msg)+"]"

func FmtValue(value)
  return _Fmt(value, ",")

// FmtSafeJoin provides a "safe" way to join an array
// of any type. It avoids the error:
func FmtSafeJoin(sep, data)
  var msg = []
  ?Type(data) ! array
    >`0,@screen.h-3@,#red,⚠ FmtSafeJoin(sep='@sep@' data='@data@')
    >`0,@screen.h-2@,#red,- data type '@Type(data)@' != 'array'
    return ""
  
  ?data.Count() = 0
    return ""
  for i = 0 .. data.Count()-1
    ?Type(data[i]) = array
      msg.Add(_Fmt(data[i], sep))
    :
      msg.Add(string.Format("{0}", data[i]))
  return string.Join(sep, msg)

func FmtBigInt(number)
  ?number > 1000
    return string.Format("{0,3:F1}K", math.BigNumber(number).ToFloat()/1000)
  ?number > 1000000
    return string.Format("{0,3:F1}M", math.BigNumber(number).ToFloat()/1000000)
  return string.Format("{0,4:D}", number)

func Fmt(data)
  return FmtSafeJoin(" ", data)

func AddColourToLogs(logs, colour)
  ?logs.Count() = 0
    return []
  var newLogs = []
  for i = 0 .. logs.Count()-1
    var l = logs[i]
    newLogs.Add([colour, l])
    // >`0,0,#yellow,adding colour @colour@ to @l@
  return newLogs

// returns an array of [color, message, remainingCooldown]
func FmtCooldownLog(weapon)
  var remaining = item.GetCooldown(weapon)
  var msg = string.Format("{0,4} {1}", remaining, weapon)
  ?!item.CanActivate()
    return [["#red", msg], remaining]
  :?remaining > 200
    return [["#red", msg], remaining]
  :?remaining > 100 & remaining <= 200
    // orange
    return [[color.FromRGB(255,102,0), msg], remaining]
  :?remaining > 0
    return [["#yellow", msg], remaining]
  :
    return [["#green", msg], remaining]

func FmtCooldownLogs(weapon_ids)
  var logs = []
  for i = 0 .. weapon_ids.Count()-1
    var formatted = FmtCooldownLog(weapon_ids[i])
    ?formatted[1] > 0
      logs.Add(formatted[0])
  return logs

// location / time / info formatting ------------

var FRAMES_PER_SECOND = 30

func FmtFramesAsTime(frame_n)
  var seconds = frame_n / FRAMES_PER_SECOND
  var framesWithinSecond = frame_n % FRAMES_PER_SECOND
  // add a padding 0 e.g. "09"
  ?framesWithinSecond <= 9
    var framesWithinSecondFmt = string.Format("0{0}", framesWithinSecond)
  :
    var framesWithinSecondFmt = string.Format("{0}", framesWithinSecond)
  // only render the minutes if needed, e.g. "1921/1:01.01"
  ?frame_n > 60 * FRAMES_PER_SECOND
    return string.Format("{0,2}:{1,2}.{2}", seconds / 60, seconds % 60, framesWithinSecondFmt)
  // the most common case, e.g. "637/21.07"
  :?frame_n > 0
    return string.Format("{0,2}.{1}",       seconds, framesWithinSecondFmt)
  // the total time is 0, e.g. "0/0:00.00"
  :
    return string.Format("{0,2}:{1,2}.{2}", 0, 0, framesWithinSecondFmt)

func FmtFrames()
  return string.Format("{0,4}/{1}", totaltime, FmtFramesAsTime(totaltime))

// weapon formatting ----------------------------

// hold shift to show weapons

func FmtItemIDBasic(item_id)
  return string.Join("\n", string.Split(item_id, ","))

var strings = import SS/Data/Strings

func _ShowWeapons(keybind)
  var matches = strings.FindAllIndexes(item.left.id, "_")
  ?key = keybind
    >`0,4,#yellow,l:(@Fmt(matches)@)\n  @FmtItemIDBasic(item.left.id)@\n\nr:@FmtItemIDBasic(item.right.id)@
    >`0,14,#white,f:@string.Join("\n", string.Break(string.Join("\n", [foe, foe.buffs.string]), 20))@

func ShowWeapons()
  _ShowWeapons(ability1)

func FmtWeaponID(weapon_id)
  // "Explosive Wand Fire wand wand_hidden_fire *10*max* +7/0 D
  // wand_hidden_fire_lv2048_Fire,element_damage_wand_2_Epic7:5-1q103
  //
  // Fire Wand Fire wand *9* +7/0 dF
  // wand_lv1024_Fire,element_damage_wand,dot_ohhit_Epic7:0-1q109

  var parts = string.Split(weapon_id, ",")
  // split up the ID into the 2 main parts,
  // "wand_hidden_fire_lv2048_Fire,element_damage_wand_2_Epic7:5-1q103"
  // -> ["wand_hidden_fire_lv2048_Fire", "element_damage_wand_2_Epic7:5-1q103"]
  ?parts.Count() < 2
    >`0,22,#yellow,c: @parts@
    return
  var craftInfo = parts[0]
  var enchantInfo = parts[1]
  >`0,22,#yellow,c: @craftInfo@
  >`0,23,#yellow,e: @enchantInfo@

  // split up craft info into 3 parts
  // "wand_hidden_fire_lv2048_Fire"
  // -> ["wand_hidden_fire", "lv2048", "Fire"]
  var parts = string.Split(craftInfo, "_")
  ?parts.Count() < 3
    >`0,24,#yellow,c: @parts@
    return
  var weapon = parts[0]
  var lvl = parts[1]
  var element = parts[2]

  // split up enchant info into 2 parts
  // "element_damage_wand_2_Epic7:5-1q103"
  // -> ["element_damage_wand_2_Epic7", "5-1q103"]
  var parts = string.Split(enchantInfo, ":")
  var enchant = parts[0]
  var suffix = parts[1]

  var enchantParts = string.Split(enchant, "_")

  return string.Join("\n", parts)


// --------------------------------------------------
var ELEMENT_FILTERS = ["Poison", "Vigor", "AEther", "Fire", "Ice", "stone",]
var SEARCH_FILTERS = [
^  "Poison", "Vigor", "Aether", "Fire", "Ice",
^  "arachnid", "serpent", "insect", "machine", "humanoid", "elemental",
^  "boss", "phase1", "phase2", "phase3",
^  "spawner", "flying", "slow", "ranged", "explode", "swarm",
^  "unpushable", "undamageable", "magic_resist", "magic_vulnerability",
^  "immune_to_stun", "immune_to_ranged", "immune_to_debuff_damage", "immune_to_physical",
^]

// returns an array of [foeName, foeID, element, filters, immunities]
func FmtFoeName(foe_name)
  ?string.Size(foe_name) = 0
    return ["", "", "", "", ""]
  var parts = string.Split(foe_name, " ")
  var tagIdx = -1

  for i = 0 .. parts.Count()-1
    ?ELEMENT_FILTERS.Contains(parts[i])
      tagIdx = i
      break

  var foe_id = parts[0]
  ?tagIdx = -1
    return [foe_id, "", "", parts, ""]
  var nameParts = []
  var filterParts = []
  var foeName = ""
  var element = parts[tagIdx]
  var immunities = []

  ?tagIdx - 1 >= 1
    for j = 1 .. tagIdx-1
      nameParts.Add(parts[j])
    foeName = string.Join(" ", nameParts)

  ?(tagIdx+1) < parts.Count()-1
    for j = tagIdx+1 .. parts.Count()-1
      ?string.IndexOf(parts[j], "immune_to_") = 0
        immunities.Add(parts[j])
      :
        filterParts.Add(parts[j])
  var allParts = [foeName, foe_id, ElementSymbol(element), string.Join(":", filterParts), string.Join(":", immunities)]

  return allParts

// --------------------------------------------------
// State Formatting

func FmtItemState(item_state)
  ?item_state = 1
    // return "idle"
    return "░"
  :?item_state = 2
    // return "cast"
    return "▒"
  :?item_state = 3
    // return "perf"
    return "█"
  :?item_state = 4
    // return "cool"
    return "▓"
  :
    return string.Format("?? {0}", item_state)

func FmtFoeState(foe_state)
  ?foe_state = 32
    return "prep (32) "
  :?foe_state = 33
    return "attack (33) "
  :?foe_state = 34
    return "complete (34) "
  :?foe_state = 2
    return "wait (2) "
  :
    return string.Format("{0} ", foe_state)  

var itemStatesFmt = [FmtItemState(1), FmtItemState(2), FmtItemState(3), FmtItemState(4)]

var seenItemStatesLeft = itemStatesFmt
var itemStateTimesLeft = []
var seenItemStatesRight = itemStatesFmt
var itemStateTimesRight = []

var seenFoeStates = []
var foeStateTimes = []

?loc.loop
  seenItemStatesLeft = itemStatesFmt
  itemStateTimesLeft = [FmtBigInt(0), FmtBigInt(0), FmtBigInt(0), FmtBigInt(0)]
  seenItemStatesRight = itemStatesFmt
  itemStateTimesRight = [FmtBigInt(0), FmtBigInt(0), FmtBigInt(0), FmtBigInt(0)]
  seenFoeStates.Clear()
  foeStateTimes.Clear()

var c1 = "#44ff00"
var c2 = "#8888ff"
var c
var n
// n = math.Lerp(n, 1, 0.02)
// c = color.Lerp(c1, c2, n)

func getColor()
  ?time % 2 = 0
    c = color.Lerp(c1, c2, math.sin(time*0.1) / 2 + 0.5)
  return c

func CollectStates(state_name, state_value, state_time, seen_states, state_times)
  var idx = seen_states.IndexOf(state_value)
  ?idx = -1
    seen_states.Add(state_value)
    state_times.Add(state_time)
  :?time % 2 = 0
    state_times.Emplace(idx, state_time)

  var lines = []
  for i = 0 .. seen_states.Count()-1
    ?i = idx
      lines.Add(["#white", string.Format("{0,3} {1}", FmtBigInt(state_times[i]), seen_states[i])])
    :
      lines.Add([getColor(), string.Format("{0,3} {1}", FmtBigInt(state_times[i]), seen_states[i])])
  return lines

var lastLeftItem = ""
var lastRightItem = ""
var lastFoe = ""

func LogItemStates()
  // item name    > left name   right name
  // state (time) > 1 (2)        1 (2)
  //              > 2 (30)       2 (30)
  //              > 3 (0)        3 (0)
  //              > 4 (5)        4 (5)

  ?lastLeftItem ! item.left.id | string.Size(item.left.id) = 0
    seenItemStatesLeft = itemStatesFmt
    itemStateTimesLeft = [0, 0, 0, 0]
    lastLeftItem = item.left.id
  :
    var leftItemLines = CollectStates(item, FmtItemState(item.left.state), item.left.time, seenItemStatesLeft, itemStateTimesLeft)
    ?leftItemLines.Count() > 0
      >`0,6,#yellow,@string.Sub(item.left, 0, math.Min(5, string.Size(item.left)))@
      for i = 0 .. leftItemLines.Count()-1
        >`0,@i+7@,@leftItemLines[i][0]@,@leftItemLines[i][1]@

  ?lastRightItem ! item.right.id | string.Size(item.right.id) = 0
    seenItemStatesRight = itemStatesFmt
    itemStateTimesRight = [0, 0, 0, 0]
    lastRightItem = item.right.id
  :
    var rightItemLines = CollectStates(item, FmtItemState(item.right.state), item.right.time, seenItemStatesRight, itemStateTimesRight)
    ?rightItemLines.Count() > 0
      >`6,6,#yellow,@string.Sub(item.right, 0, math.Min(5, string.Size(item.right)))@
      for i = 0 .. rightItemLines.Count()-1
        >`6,@i+7@,@rightItemLines[i][0]@,@rightItemLines[i][1]@

func LogFoeStates(x_offset)
  ?lastFoe ! foe.id | string.Size(foe.id) = 0
    seenFoeStates.Clear()
    foeStateTimes.Clear()
    lastFoe = foe.id
  :
    var foeLines = CollectStates(foe, FmtFoeState(foe.state), foe.time, seenFoeStates, foeStateTimes)
    LogTopRight(x_offset, foeLines)

func cleanBuffString(b, buff_type)
  var idx = string.IndexOf(b, buff_type)
  var buffLen = string.Size(buff_type)
  ?idx = -1
    return b
  
  // it's a buff_: [idx: 0, len: ->]
  ?idx = 0
    return string.Sub(b, buffLen+1)
  // there's a _ before the buff name
  ?string.IndexOf(b, "_", idx-1) ! idx-1
    return b
  // it's a _debuff: [idx: suffix, len: total - length of suffix]
  ?idx-1 = string.Size(b)-(buffLen+1)
    return string.Sub(b, 0, idx-1)
  // it's a _debuff_ in the middle: [idx: suffix, len: total - length of suffix]
  return string.Sub(b, idx+(buffLen+1))

// Formats a string of buffs or debuffs, returning 
// - an array of individual buffs or debuffs,
func FmtBuffs(buff_string, buff_type)
  var bfs = []
  var msgs = []

  // check if there are any buffs
  ?string.Size(buff_string) = 0 | string.Equals(buff_string, ",")
    return []
  :
    // if there is only one buff, it won't have a comma
    ?string.IndexOf(buff_string, ",") = -1
      bfs = [buff_string]
    :
      // split the string into an array of buffs
      bfs = string.Split(buff_string, ",")

  var t = ""
  var lvl = ""
  var b = ""
  var symbol = ""
  var buffTypeLen = string.Size(buff_type)
  ?bfs.Count() = 0
    return []

  // for each buff
  for i = 0 .. bfs.Count()-1
    b = ""
    // if the buff is formatted as
    // "buff:buff_name:level:time_remaining"
    ?string.Size(bfs[i]) = 0
      continue

    ?string.IndexOf(bfs[i], ":") ! -1
      var parts = string.Split(bfs[i], ":")
      symbol = parts[0]
      b = parts[1]
      lvl = parts[2]
      t = parts[3]
    :
      // otherwise, the buff is formatted as
      // "buff_string:time_remaining"
      b = bfs[i]
      t = ""
    ?string.Size(b) = 0
      continue

    b = cleanBuffString(b, buff_type)
    msgs.Add(string.Format("{0}·{1} {2}:{3}", symbol, lvl, b, t))
    continue

  ?msgs.Count() = 0
    return []

  return msgs

func FmtBossHealth()
  var hpPercent = 0
  ?foe = boss
    ?foe.hp > 0
      hpPercent = foe.hp*100/foe.maxhp
    :
      return ""
  :
    return ""
  return string.Format("{0}/{1} {2}%", foe.hp, foe.maxhp, hpPercent)


// --------------------------


func LogAllBuffsAndDebuffs()
  // [colour,msg] tuples
  var playerBuffs = AddColourToLogs(FmtBuffs(buffs.string, "buff"), "#green")
  var playerDebuffs = AddColourToLogs(FmtBuffs(debuffs.string, "debuff"), "#red")

  var foeBuffs = AddColourToLogs(FmtBuffs(foe.buffs.string, "buff"), "#green")
  var foeDebuffs = AddColourToLogs(FmtBuffs(foe.debuffs.string, "debuff"), "#red")

  for part : string.Split(foe, " ")
    ?part = "immune"
      var immunity = string.Sub(part, string.Size("immune_to_"))
      foeBuffs.Add([
      ^  "#yellow",
      ^  string.Format("{0} {1}:{2}", "x", immunity, "∞"),
      ^])
 
  LogAbovePlayer(playerBuffs,   -3, -2, -1, "right")
  LogAbovePlayer(playerDebuffs, -3, -1,  1, "right")
  LogAbovePlayer(foeBuffs,       4, -2, -1, "left")
  LogAbovePlayer(foeDebuffs,     4, -1,  1, "left")

// --------------------------

func LogFoeDistance()
  ?foe.distance = 9999
    return
  LogAbovePlayer([["#yellow", foe.distance]], 0, -2, -1, "left")

// --------------------------

func LogPickupDistance()
  ?pickup.distance = 9999
    return
  LogAbovePlayer([["#yellow", string.Format("{0}:{1,3}", pickup, pickup.distance)]], 0, -1, -1, "left")

func LogHarvestDistance()
  ?harvest.distance = 9999
    return
  LogAbovePlayer([["#yellow", string.Format("{0}:{1,3}", harvest, harvest.distance)]], 0, 0, -1, "left")

// --------------------------

var mostRecentStates = [["","",""], ["","",""]]
var currentHealth = maxhp
var hpLogs = []

?loc.loop
  mostRecentStates = [["","",""], ["","",""]]
  currentHealth = maxhp
  hpLogs.Clear()

func LogStatesBeforeDamage()
  ?hpLogs.Count() > 0
    for i = 0 .. hpLogs.Count()-1
      LogAbovePlayer([["#red", hpLogs[i]]], 0, -3-i, -1, "left")

  ?hpLogs.Count() > 3
    hpLogs.RemoveAt(0)

  // state is the same, don't log
  ?structs.ArraysEqual(mostRecentStates[0], [foe.state, foe.time])
    return
  mostRecentStates[1] = mostRecentStates[0]
  mostRecentStates[0] = [foe.state, foe.time]

  ?hp = currentHealth
    return

  ?hp < currentHealth
    var hpLog = string.Format("{0}: {1} ({2})>{3} ({4})", hp-currentHealth, mostRecentStates[1][0], mostRecentStates[1][1], foe.state, foe.time)
    ?hpLogs.Contains(hpLog) = false
      hpLogs.Add(hpLog)
    currentHealth = hp
    return

  ?hp > currentHealth
    currentHealth = hp

// --------------------------

var AAC_ENABLED = true
var CL_ORANGE = color.FromRGB(255,102,0)

// --------------------------

func LogAAC(aac_enabled)
  AAC_ENABLED = aac_enabled

func WriteLogs()
  LogTopLeft(10, FmtCooldownLogs([
  ^  "bardiche", "quarterstaff", "heavy_hammer",
  ^  "bash", "dash",
  ^  "mind", "hatchet",
  ^  "staff_aether", "staff_fire", "staff_ice", "staff_poison", "staff_stone", "staff_vigor",
  ^  "skeleton_arm", "mask", "blade",
  ^  "wand_aether", "wand_fire", "wand_ice", "wand_poison", "wand_vigor",
  ^  "talisman_aether", "talisman_fire",
  ^  "cinderwisp", "voidweaver"
  ^]))
  LogTopLeft(1, [
    ["#white",  string.Format("{0}: {1}", "t", Fmt([FmtFrames(), "gp:", loc.gp]))],
    ["#white",  string.Format("{0}: {1}", "p", Fmt([pos.x, screen.FromWorldX(pos.x), screen.FromWorldZ(pos.z - pos.y)]))],
    ["#blue",   string.Format("{0}: {1}", "key", Fmt([string.Break(key, 15)[0]]))],
  ])

  LogTopRight(0, [["#green", Fmt([foe.state, foe.time])]])

  // log buffs and debuffs above the player
  LogItemStates()

  var FoeInfo = FmtFoeName(foe)
  var FoeInfoFmt = [
  ^  ["#green", string.Format("'{0}'", FoeInfo[0])],
  ^  ["#white", string.Format("{0} {1}", FoeInfo[1], FoeInfo[2])],
  ^  ["#green", Fmt([FmtBossHealth()])],
  ^  ["#white", Fmt([FoeInfo[3]])],
  ^  ["#yellow", Fmt([FoeInfo[4]])],
  ]
  LogTopRight(0, FoeInfoFmt)
  LogFoeStates(7)
  LogLowerRight(1, [
  ^ ["#yellow", string.Format("{0}/{1}", item.GetTreasureCount(), item.GetTreasureLimit())], 
  ^])

  LogAllBuffsAndDebuffs()
  LogFoeDistance()
  LogPickupDistance()
  LogHarvestDistance()

  // LogStatesBeforeDamage()